plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id "pl.allegro.tech.build.axion-release" version "1.13.6"
}

group 'io.github.valentin-goncharov'
project.version = scmVersion.version

scmVersion {
    tag {
        prefix = ''
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.github.tomakehurst:wiremock-jre8:2.32.0"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    testImplementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'org.xmlunit:xmlunit-assertj:2.9.0'
    testImplementation 'io.mockk:mockk:1.12.2'
    testImplementation "io.ktor:ktor-client-java:1.6.7"
    testImplementation "io.ktor:ktor-client-auth:1.6.7"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0'

}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'wiremock-kotlin-dsl'

            from components.java

            pom {
                name = 'WireMock Kotlin DSL'
                description = 'Kotlin DSL fro WireMock library'
                url = 'https://github.com/valentin-goncharov/wiremock-kotlin-dsl'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'valentin-goncharov'
                        name = 'Valentin Goncharov'
                        email = 'goncharov.valentin@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:valentin-goncharov/wiremock-kotlin-dsl.git'
                    developerConnection = 'scm:git:ssh://github.com:valentin-goncharov/wiremock-kotlin-dsl.git'
                    url = 'https://github.com/valentin-goncharov/wiremock-kotlin-dsl'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = "6c056bced4ee00"
            nexusUrl = uri("https://s01.oss.sonatype.org/service/local/")
            snapshotRepositoryUrl = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            username = System.getenv("MAVEN_USERNAME")
            password = System.getenv("MAVEN_PASSWORD")
        }
    }
}

signing {

    def key = System.getenv("GPG_KEY")
    def password = System.getenv("GPG_PASSPHRASE")

    useInMemoryPgpKeys(key, password)

    sign publishing.publications.mavenJava
}